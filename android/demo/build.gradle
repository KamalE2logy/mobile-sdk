buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
    }
}

apply plugin: 'com.android.application'

// Need to set this repository's code block to load the local AAR file from the libs directory
repositories {
	flatDir {
		dirs 'libs'
	}
}

dependencies {
	// The following are the dependencies needed to run MappedIn SDK
	compile 'com.android.support:support-v4:20.0.0'
	compile 'com.android.support:appcompat-v7:20.0.0'
	compile 'com.android.support:gridlayout-v7:20.0.0'
	compile 'com.google.android.gms:play-services-maps:6.5.87'
	compile 'com.flaviofaria:kenburnsview:1.0.6'
	compile 'com.squareup.okhttp:okhttp:2.0.0'
	compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
	compile 'com.squareup.okio:okio:1.0.0'
	compile 'com.squareup.picasso:picasso:2.3.2'

	// Loading the MappedIn SDK
	compile 'com.mappedin:MappedIn-SDK-0.1@aar'
}

android {
    compileSdkVersion 19
    buildToolsVersion "21.1.2"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
    }
}

task startDemo(type: Exec) {
    dependsOn 'installDebug'
    def adb = new File("${System.env.ANDROID_HOME}", "platform-tools${File.separator}adb")
    commandLine adb, "shell", "am", "start", "-n", "com.example.mappedin_demo/.ExampleActivity"
}
